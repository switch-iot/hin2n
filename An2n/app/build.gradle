apply plugin: 'com.android.application'


def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always', '--dirty', '--match', 'hin2n_v*'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

def getEnvOrConsole = { env, prompt ->
    def val = null
    try {
        val = System.getenv(env)
        if (val == null || val == "") {
            if (System.console() != null) {
                val = System.console().readLine("\n" + prompt + ": ")
            }
        }
    } catch (ignored) {
        return null
    }
    return val
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "wang.switchy.an2n"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode getVersionCode()
        versionName getVersionName()
        multiDexEnabled true //突破方法数65535的限制
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "normal", ARCH: "all", UMENG_APPKEY_VALUE: ""]
        resConfigs "en", "zh-rCN", "zh-rTW" // 多语言

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    //添加我们的签名文件配置 注意：signingConfigs要写在buildTypes
    signingConfigs {
        debug {}

        release {
            //为我们的release包添加签名文件配置
            storeFile file("An2n.jks")
            storePassword getEnvOrConsole("AN2N_KSTOREPWD", "Keystore password")
            keyAlias "An2n"
            keyPassword getEnvOrConsole("AN2N_KEYPWD", "Key password")
        }
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "API_DEBUG", "true")
            minifyEnabled false //是否启用混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }

        release {
            buildConfigField("boolean", "API_DEBUG", "false")
            minifyEnabled false //是否启用混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    //配置自定义打包名称
    applicationVariants.all { variant ->
        def umeng_appkey = getEnvOrConsole("UMENG_APPKEY", "UMENG appkey")
        if (umeng_appkey == null) {
            umeng_appkey = ""
        }
        variant.mergedFlavor.manifestPlaceholders += [UMENG_APPKEY_VALUE: umeng_appkey]

        variant.outputs.each { output ->
            def outputFile = output.outputFile
            def fileName = null
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                variant.mergedFlavor.versionName = getVersionName()
                def channel = variant.mergedFlavor.manifestPlaceholders.UMENG_CHANNEL_VALUE
                def arch = variant.mergedFlavor.manifestPlaceholders.ARCH
                fileName = "${variant.mergedFlavor.versionName}_${channel}_${arch}_${variant.buildType.name}.apk"
				output.outputFile = new File(output.outputFile.parent, fileName)
            }
        }
    }

    flavorDimensions "channel", "arch"

    productFlavors {
        normal {
            dimension "channel"
            manifestPlaceholders += [UMENG_CHANNEL_VALUE: "normal"]
        }
        arm {
            dimension "arch"
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"
                manifestPlaceholders += [ARCH: "arm"]
            }
        }
        x86 {
            dimension "arch"
            ndk {
                abiFilters "x86", "x86_64"
                manifestPlaceholders += [ARCH: "x86"]
            }
        }
    }

//    productFlavors.all {
//        flavor -> flavor.manifestPlaceholders += [UMENG_CHANNEL_VALUE: name]
//    }


    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:design:25.2.0'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.+'
    compile 'org.greenrobot:eventbus:3.1.1'

    //集成调试库，能够帮助用户发现埋点错误，上线前务必删除集成调试库，避免无关信息的打印
    compile 'com.umeng.sdk:common:latest.integration'
    compile 'com.umeng.sdk:utdid:1.1.5.3'
    compile 'com.umeng.sdk:analytics:latest.integration'
}
